plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
}

apply from: rootProject.file('common.gradle')

android {
    namespace 'app.simple.felicity'

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    defaultConfig {
        applicationId "app.simple.felicity"

        versionCode 2
        versionName "0.0.2-preview"
    }

    signingConfigs {
        release {
            def properties = loadProperties(rootProject.file('local.properties').absolutePath)
            if (properties.isEmpty()) {
                logger.error("local.properties file not found or empty, retrying with environment variables.")
                storeFile = null
            }

            def keystoreFile = findKeystoreFile(properties)

            if (keystoreFile.exists()) {
                storeFile = keystoreFile
                storePassword = getEnvOrProperty("SIGNING_STORE_PASSWORD", properties)
                keyAlias = getEnvOrProperty("SIGNING_KEY_ALIAS", properties)
                keyPassword = getEnvOrProperty("SIGNING_KEY_PASSWORD", properties)
            } else {
                storeFile = null
                logger.error("Keystore file not found, signing disabled.")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            dependenciesInfo {
                includeInApk false
                includeInBundle false
            }

            if (signingConfigs.release != null && signingConfigs.release.storeFile != null) {
                signingConfig = signingConfigs.release
            }
        }
        debug {
            versionNameSuffix "-dev"

            if (signingConfigs.release != null && signingConfigs.release.storeFile != null) {
                signingConfig = signingConfigs.release
            }
        }
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }

    kotlin {
        compilerOptions {
            freeCompilerArgs.add("-XXLanguage:+PropertyParamAnnotationDefaultTargetMode")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':theme')
    implementation project(':decorations')
    implementation project(':preferences')
    implementation project(':core')
    implementation project(':shared')
    implementation project(':repository')
    implementation project(':engine')

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    // AndroidX
    implementation 'androidx.fragment:fragment-ktx:1.8.9'
    implementation 'androidx.activity:activity-ktx:1.12.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.10.0'
    implementation 'androidx.documentfile:documentfile:1.1.0'
    implementation 'androidx.media:media:1.7.1'
    implementation 'androidx.security:security-crypto:1.1.0'
    implementation 'androidx.media3:media3-exoplayer:1.9.2'
    implementation 'androidx.media3:media3-ui:1.9.2'
    implementation 'androidx.media3:media3-common:1.9.2'
    implementation 'androidx.media3:media3-session:1.9.2'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.asynclayoutinflater:asynclayoutinflater:1.1.0'

    implementation "com.google.dagger:hilt-android:2.59.2"
    ksp "com.google.dagger:hilt-android-compiler:2.59.2"

    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    // Google
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Third party

    // Toolkit
    implementation 'com.github.Hamza417:renderscript-intrinsics-replacement-toolkit:4ae2409809'

    // Glide
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    implementation 'com.github.bumptech.glide:okhttp3-integration:5.0.5'
    ksp 'com.github.bumptech.glide:ksp:5.0.5'

    // Room
    implementation "androidx.room:room-ktx:2.8.4"
    ksp "androidx.room:room-compiler:2.8.4"
    androidTestImplementation "androidx.room:room-testing:2.8.4"
}

static def loadProperties(filePath) {
    def properties = new Properties()
    def file = new File(filePath)
    if (file.exists()) {
        properties.load(new FileInputStream(file))
    } else {
        println("Properties file not found: $filePath")
    }
    return properties
}

static def findKeystoreFile(properties) {
    def path = System.getProperty("user.home") + "/work/_temp/keystore/"
    def fallbackPath = properties.get("KEYSTORE_PATH")
    def keystoreFileName = "key.jks"
    def keystoreFile = new File(path, keystoreFileName)

    if (!keystoreFile.exists() && fallbackPath != null) {
        keystoreFile = new File(fallbackPath)
    }

    return keystoreFile
}

static def getEnvOrProperty(envKey, properties) {
    return System.getenv(envKey) ?: properties.get(envKey)
}